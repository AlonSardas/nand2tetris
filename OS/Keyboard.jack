// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int keyboardAddress;
    static int lineWidth;

    /** Initializes the keyboard. */
    function void init() {
        let keyboardAddress = 24576;
        let lineWidth = 56;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(keyboardAddress);
    }

    /**	Waits until a key is pressed on the keyboard and released,
     *  then echoes the key to the screen, and returns the character 
     *  of the pressed key. */
    function char readChar() {
        var char key, newKey;

        while (key = 0) {
            let key = Memory.peek(keyboardAddress);
        }

        if (~(key = String.backSpace())) {
            do Output.printChar(key);
        }

        let newKey = key;

        while (newKey = key) {
            let newKey = Memory.peek(keyboardAddress);
        }

        return key;
    }

    /**	Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its value. Also handles user backspaces. */
    function String readLine(String message) {
        var char c, newLine, backspace;
        var String str;

        let str = String.new(lineWidth);
        let newLine = String.newLine();
        let backspace = String.backSpace();
        do Output.printString(message);

        while (true) {
            let c = Keyboard.readChar();
            if (c = newLine) {
                return str;
            }
            if (c = backspace) {
                if (str.length() > 0) {
                    do Output.printChar(c);  // Delete the character only if it is a character from the string
                    do str.eraseLastChar();
                }
            } else {
                do str.appendChar(c);
            }
        }

        return null;
    }   

    /** Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its integer value (until the first non-digit character in the
     *  entered text is detected). Also handles user backspaces. */
    function int readInt(String message) {
        var String str;
        var int ans;

        let str = Keyboard.readLine(message);
        let ans = str.intValue();
        do str.dispose();
        return ans;
    }
}
